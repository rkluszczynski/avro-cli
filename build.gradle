plugins {
    id 'java'
    id 'groovy'

    id 'checkstyle'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'

    id 'org.springframework.boot' version '1.5.4.RELEASE'
    id 'pl.allegro.tech.build.axion-release' version '1.7.1'
}

scmVersion {
    tag.prefix = project.name

    hooks {
        pre 'fileUpdate',
                [file: 'docs/index.md', pattern: { v, c -> /avro-cli-$v/ }, replacement: { v, c -> "avro-cli-$v" }]
    }
}

group = 'io.github.rkluszczynski'
project.version = scmVersion.version

sourceCompatibility = 1.8
targetCompatibility = 1.8

def versions = [
        avro        : '1.8.2',
        spring_kafka: '1.2.2.RELEASE',
        spock       : '1.1-groovy-2.4',

        checkstyle  : '6.3',
        gradle      : '4.0'
]

dependencies {
    compile 'org.springframework.boot:spring-boot-starter',
            "org.springframework.kafka:spring-kafka:${versions.spring_kafka}",
            "org.apache.avro:avro:${versions.avro}",
            'com.beust:jcommander:1.72',
            'tech.allegro.schema.json2avro:converter:0.2.5'

    testCompile 'com.github.tomakehurst:wiremock:2.6.0',
            'cglib:cglib-nodep:3.2.5',
            'org.objenesis:objenesis:2.6',
            "org.spockframework:spock-core:${versions.spock}",
            "org.spockframework:spock-spring:${versions.spock}",
            'org.springframework.boot:spring-boot-starter-test',
            "org.springframework.kafka:spring-kafka-test:${versions.spring_kafka}"
}

repositories {
    mavenCentral()
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

configurations {
    checkstyleConfig {
        transitive = false
    }
}

dependencies {
    checkstyleConfig "com.puppycrawl.tools:checkstyle:${versions.checkstyle}"
}

checkstyle {
    toolVersion = "${versions.checkstyle}"
//    config = resources.text.fromArchiveEntry(configurations.checkstyleConfig, 'google_checks.xml')
}

task wrapper(type: Wrapper) {
    gradleVersion = "${versions.gradle}"
}
